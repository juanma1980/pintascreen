' Gambas class file

Inherits Dibujo

Private mouseinicioX As Integer
Private mouseinicioY As Integer
Private mousefinalX As Integer
Private mousefinalY As Integer
Public hImage As Image
Private puntofin As Boolean = False
Private hBrush As PaintBrush

Public Sub _new(...)

  mouseinicioX = param[0]
  mouseinicioY = param[1]

End

Public Sub dibuja()

  If puntofin = True Then

    hBrush = Paint.Image(hImage)

    Paint.Brush = hBrush
    Paint.Rectangle(mouseinicioX - 2, mouseinicioY - 2, mousefinalX - mouseinicioX + 4, mousefinalY - mouseinicioY + 4)
    Paint.Fill
  Endif

End

Public Sub ptofinal(x As Integer, y As Integer)

  mousefinalX = x
  mousefinalY = y
  comprobarOrdenCoordenadas()

  ' hImage = Image.Load(  & user.home & "/.config/pintascreen/" & User.Name & "/fondo.jpg")
  puntofin = True

End

Private Function comprobarOrdenCoordenadas()

  Dim valortmp As Integer

  If mousefinalX <= mouseinicioX Then

    valortmp = mouseinicioX
    mouseinicioX = mousefinalX
    mousefinalX = valortmp

  Endif

  If mousefinalY <= mouseinicioY Then

    valortmp = mouseinicioY
    mouseinicioY = mousefinalY
    mousefinalY = valortmp

  Endif

End

Public Sub setImagen(imagen As Image)

  hImage = imagen
  'ruta: el valor es el mismo al fichero /home/" & user.home & "/.config/pintascreen/fondo.jpg

End

Public Sub guardar() As String

  Dim contenido As String

  contenido = "dibujo|Aclara|"
  contenido &= Str$(mouseinicioX) & "|"
  contenido &= Str$(mouseinicioY) & "|"
  contenido &= Str$(mousefinalX) & "|"
  contenido &= Str$(mousefinalY) & "|"
  contenido &= Str$(puntofin) & "|"

  Return contenido

End

Public Function abrir(Arraylinea As String[]) As Boolean

  If Arraylinea[1] = "Aclara" Then

    mouseinicioX = Val(Arraylinea[2])
    mouseinicioY = Val(Arraylinea[3])
    mousefinalX = Val(Arraylinea[4])
    mousefinalY = Val(Arraylinea[5])
    puntofin = Val(Arraylinea[6])
    hImage = Image.Load(user.home & "/.config/pintascreen/fondo.jpg")
    Return True
  Else
    Return False

  Endif

End
